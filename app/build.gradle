apply plugin: 'com.android.application'
apply plugin: 'com.jakewharton.butterknife'
android {
    compileSdkVersion 27
    buildToolsVersion "27.0.2"
    defaultConfig {
        applicationId "com.duma.ld.zhilianlift"
        minSdkVersion 19
        targetSdkVersion 27
        versionCode 2
        versionName "1.1"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        ndk {
            abiFilters 'armeabi', 'armeabi-v7a', 'armeabi-v8a', 'x86', 'x86_64'
        }
    }
    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
    }
    signingConfigs {
        release {

        }
    }
    buildTypes {
        release {
            minifyEnabled true
            // Zipalign优化
            zipAlignEnabled true
            // 移除无用的resource文件 资源收缩器不能用于库。
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            minifyEnabled true
            // Zipalign优化
            zipAlignEnabled true
            //只优化不混淆
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    //修改生成的最终文件名
    applicationVariants.all { variant ->
        variant.outputs.all {
            def apkName = '智联生活' + '_v' + variant.versionName + '_' + new Date().format("yyyy-MM-dd HH:mm:ss", TimeZone.getTimeZone("GMT+8"))
            if (variant.buildType.name == "release") {
                apkName = apkName + '_release'
            } else {
                apkName = apkName + '_debug'
            }
            outputFileName = apkName + '.apk'
        }
    }
}

//读取key
File propFile = file('signing.properties');
if (propFile.exists()) {
    def Properties props = new Properties()
    props.load(new FileInputStream(propFile))
    if (props.containsKey('STORE_FILE') && props.containsKey('STORE_PASSWORD') &&
            props.containsKey('KEY_ALIAS') && props.containsKey('KEY_PASSWORD')) {
        android.signingConfigs.release.storeFile = file(props['STORE_FILE'])
        android.signingConfigs.release.storePassword = props['STORE_PASSWORD']
        android.signingConfigs.release.keyAlias = props['KEY_ALIAS']
        android.signingConfigs.release.keyPassword = props['KEY_PASSWORD']
    } else {
        android.buildTypes.release.signingConfig = null
    }
} else {
    android.buildTypes.release.signingConfig = null
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
    implementation project(':baselibrary')
    implementation project(':maplibrary')
    //银联
    implementation files('libs/UPPayAssistEx.jar')
    implementation files('libs/UPPayPluginExPro.jar')
    implementation 'com.android.support:cardview-v7:27.1.0'
    implementation 'com.google.code.gson:gson:2.8.1'
    //权限
    implementation 'com.yanzhenjie:permission:1.1.2'
    //数据库
    implementation 'org.litepal.android:core:1.6.0'
    //插件
    implementation 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'
    //bottomBar
    implementation 'com.github.ittianyu:BottomNavigationViewEx:1.2.4'
    //带数字的小红点
    implementation 'q.rorbin:badgeview:1.1.0'
    implementation 'com.android.support:design:27.1.0'
    //选择城市索引
    implementation 'me.yokeyword:indexablerecyclerview:1.3.0'
    //可伸缩布局
    implementation 'com.google.android:flexbox:0.3.1'
    //数据库
    implementation 'org.litepal.android:core:1.6.0'
    //图片拍照选择器
    implementation 'com.github.LuckSiege.PictureSelector:picture_library:v2.2.3'
    //时间选择器
    implementation 'com.contrarywind:Android-PickerView:3.2.7'
    //popupWindow
    implementation 'com.github.razerdp:BasePopup:1.8.5.1'
    //banner
    implementation 'com.youth.banner:banner:1.4.10'
    //状态栏
    implementation 'com.jaeger.statusbarutil:library:1.4.0'
    //webview
    implementation 'com.just.agentweb:agentweb:3.1.0'
    //二维码
    implementation 'cn.bingoogolapple:bga-qrcode-zbar:1.2.1'
    //支付宝
    implementation files('libs/alipaySdk-20170922.jar')
    //微信
    //    implementation 'com.tencent.mm.opensdk:wechat-sdk-android-with-mta:5.1.0'
    implementation 'com.tencent.mm.opensdk:wechat-sdk-android-without-mta:+'
    //刷新
    implementation 'com.scwang.smartrefresh:SmartRefreshLayout:1.0.5.1'

}
